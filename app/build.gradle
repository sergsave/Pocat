apply plugin: 'com.android.application'
apply plugin: 'com.google.gms.google-services'
apply plugin: 'com.google.firebase.crashlytics'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlinx-serialization'
apply plugin: 'kotlin-kapt'

android {
    compileSdkVersion 29
    buildToolsVersion "29.0.3"

    androidExtensions {
        experimental = true
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }

    defaultConfig {
        applicationId "com.sergsave.pocat"
        minSdkVersion 21
        targetSdkVersion 29
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        vectorDrawables.useSupportLibrary = true

        kotlinOptions {
            jvmTarget = "1.8"
            freeCompilerArgs += [
                    "-Xuse-experimental=kotlinx.serialization.ImplicitReflectionSerializer"
            ]
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    flavorDimensions "version"
    productFlavors {
        def appName = "Pocat"

        development {
            dimension "version"
            applicationIdSuffix ".dev"
            versionNameSuffix "_dev"
            resValue "string", "app_name", "$appName"
            resValue "string", "app_label", "$appName Dev"
            buildConfigField("String", "DYNAMIC_LINK_DOMAIN", "\"https://pocatappdev.page.link\"")
            buildConfigField("Boolean", "HIDE_TEST_ACTION", "false")
            manifestPlaceholders = [firebaseProjectId:"pocat-app-dev"]
        }
        production {
            dimension "version"
            resValue "string", "app_name", "$appName"
            resValue "string", "app_label", "$appName"
            buildConfigField("String", "DYNAMIC_LINK_DOMAIN", "\"https://pocatapp.page.link\"")
            buildConfigField("Boolean", "HIDE_TEST_ACTION", "true")
            manifestPlaceholders = [firebaseProjectId:"pocat-app"]
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

}

dependencies {
    def room_version = "2.2.5"

    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.jetbrains.kotlinx:kotlinx-serialization-runtime:0.20.0"
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'androidx.core:core-ktx:1.2.0'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    implementation 'androidx.lifecycle:lifecycle-extensions:2.2.0'
    implementation "androidx.fragment:fragment-ktx:1.2.2"
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test.ext:junit:1.1.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'
    implementation 'androidx.recyclerview:recyclerview:1.1.0'
    implementation "androidx.viewpager2:viewpager2:1.0.0"
    implementation "androidx.room:room-runtime:$room_version"
    implementation "androidx.room:room-rxjava2:$room_version"
    implementation 'androidx.recyclerview:recyclerview-selection:1.0.0'
    implementation "androidx.preference:preference-ktx:1.1.1"
    implementation 'de.hdodenhof:circleimageview:3.1.0'
    implementation 'com.google.android.material:material:1.2.0-alpha05'
    implementation 'com.github.bumptech.glide:glide:4.11.0'
    // migrate to rxjava3 after stable room version with rxjava3
    implementation "io.reactivex.rxjava2:rxjava:2.2.19"
    implementation "io.reactivex.rxjava2:rxkotlin:2.4.0"
    implementation "io.reactivex.rxjava2:rxandroid:2.1.1"
    implementation platform('com.google.firebase:firebase-bom:26.1.0')
    implementation 'com.google.firebase:firebase-storage-ktx'
    implementation 'com.google.firebase:firebase-auth-ktx'
    implementation 'com.google.firebase:firebase-dynamic-links-ktx'
    implementation 'com.google.firebase:firebase-crashlytics-ktx'
    implementation 'com.google.firebase:firebase-analytics-ktx'
    implementation 'de.psdev.licensesdialog:licensesdialog:2.1.0'

    debugImplementation 'com.squareup.leakcanary:leakcanary-android:2.4'

    implementation project(":ringdroid")

    kapt 'com.github.bumptech.glide:compiler:4.11.0'
    kapt "androidx.room:room-compiler:$room_version"
}
